<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 3.2//EN">
<HTML>
 <HEAD>
   <TITLE> [antlr-interest] Parser --&gt; TreeParser: help with bigger example	please?
   </TITLE>
   <LINK REL="Index" HREF="index.html" >
   <LINK REL="made" HREF="mailto:antlr-interest%40antlr.org?Subject=Re:%20%5Bantlr-interest%5D%20Parser%20--%3E%20TreeParser%3A%20help%20with%20bigger%20example%0A%09please%3F&In-Reply-To=%3Cb6077b530705171201s6fef3a1aj31808fe2bdfc5dce%40mail.gmail.com%3E">
   <META NAME="robots" CONTENT="index,nofollow">
   <META http-equiv="Content-Type" content="text/html; charset=us-ascii">
   <LINK REL="Previous"  HREF="020712.html">
   <LINK REL="Next"  HREF="020714.html">
 </HEAD>
 <BODY BGCOLOR="#ffffff">
   <H1>[antlr-interest] Parser --&gt; TreeParser: help with bigger example	please?</H1>
    <B>Tim Clark</B> 
    <A HREF="mailto:antlr-interest%40antlr.org?Subject=Re:%20%5Bantlr-interest%5D%20Parser%20--%3E%20TreeParser%3A%20help%20with%20bigger%20example%0A%09please%3F&In-Reply-To=%3Cb6077b530705171201s6fef3a1aj31808fe2bdfc5dce%40mail.gmail.com%3E"
       TITLE="[antlr-interest] Parser --&gt; TreeParser: help with bigger example	please?">timgclark at gmail.com
       </A><BR>
    <I>Thu May 17 12:01:43 PDT 2007</I>
    <P><UL>
        <LI>Previous message: <A HREF="020712.html">[antlr-interest] Filtering using ANTLR
</A></li>
        <LI>Next message: <A HREF="020714.html">[antlr-interest] Parser --&gt; TreeParser: help with bigger	exampleplease?
</A></li>
         <LI> <B>Messages sorted by:</B> 
              <a href="date.html#20713">[ date ]</a>
              <a href="thread.html#20713">[ thread ]</a>
              <a href="subject.html#20713">[ subject ]</a>
              <a href="author.html#20713">[ author ]</a>
         </LI>
       </UL>
    <HR>  
<!--beginarticle-->
<PRE>Hi All
The book gives a rather simple (and easy to understand) example of the step
from the parser to the tree parser (for expressions). When it's a lot more
complicated (as below, for example) I'm a little stumped as to how to do it.
Any words of advice would be greatly appreciated. Intuitively it's clear
that one ends up with lists of simple things like ^('+' arg1 arg2) or ^('-'
arg), but it's a bit mind-boggling when the rules are so much more
complicated.

expr
    :
    assignExpr
    ;

assignExpr
    :
    condExpr (assignOp^ assignExpr)?
    ;

assignOp
    :
    ASSIGN | PLUS_ASSIGN | MINUS_ASSIGN | STAR_ASSIGN | DIV_ASSIGN
    | MOD_ASSIGN | CAT_ASSIGN | OR_ASSIGN | AND_ASSIGN | XOR_ASSIGN |
LCAT_ASSIGN
    ;

condExpr
    :
    condOrExpr (QUESTION^ expr COLON expr)?
    ;

condOrExpr
    :
    condAndExpr (LOGICAL_OR^ condAndExpr)*
    ;

condAndExpr
    :
    eqExpr (LOGICAL_AND^ eqExpr)*
    ;

eqExpr
    :
    relExpr ((IS_EQUAL^ | NOT_EQUAL^) relExpr)*
    ;

relExpr
    :
    incOrExpr ((LESS_THAN^ | GREATER_THAN^ | LESS_OR_EQUAL^ |
GREATER_OR_EQUAL^) incOrExpr)*
    ;

incOrExpr
    :
    excOrExpr (LINE^ excOrExpr)*
    ;

excOrExpr
    :
    andExpr (HAT^ andExpr)*
    ;

andExpr
    :
    shiftExpr (AMPER^ shiftExpr)*
    ;

shiftExpr
    :
    addExpr ((LSHIFT^ | RSHIFT^) addExpr)*
    ;

addExpr
    :
    multExpr ((PLUS^ | MINUS^ | CAT^ | LIST_CAT^) multExpr)*
    ;

multExpr
    :
    unaryExpr ((STAR^ | DIV^ | MOD^) unaryExpr)*
    ;

unaryExpr
    :
    primaryExpr
    |
    negation
    |
    binaryComp
    |
    logicalNot
    |
    sizeOf
    ;

negation
    :
    NEGATE unaryExpr
    -&gt; ^(NEG unaryExpr)
    ;

binaryComp
    :
    COMPLEMENT unaryExpr
    -&gt; ^(COMPL unaryExpr)
    ;

logicalNot
    :
    LOGICAL_NOT unaryExpr
    -&gt; ^(NOT unaryExpr)
    ;

sizeOf
    :
    SIZE unaryExpr
    -&gt; ^(SIZEOF unaryExpr)
    ;

primaryExpr
options {backtrack=true;}
    :
    constant
    |
    ID
    |
    parenExpr
    |
    funcCall
    |
    listSelector
    |
    listCons
    |
    mapCons
    |
    preInc
    |
    postInc
    |
    preDec
    |
    postDec
    ;

parenExpr
    :
    LPAREN! expr RPAREN!
    ;

exprList
    :
    expr (COMMA! expr)*
    |
    // Empty
    ;

funcCall
    :
    ID LPAREN exprList RPAREN
    -&gt; ^(CALL ID exprList)
    ;

listCons
    :
    LBRAK exprList RBRAK
    -&gt; ^(LIST_CONS exprList)
    ;

listSelector
    :
    ID sliceList
    -&gt; ^(LIST_SEL ID sliceList)
    ;

sliceList
    :
    listSlice (listSlice)*
    ;

listSlice
    :
    LBRAK! expr RBRAK!
    ;

mapCons
    :
    '{{' keyValueList '}}'
    -&gt; ^(MAP_CONS keyValueList)
    ;

keyValueList
    :
    keyValuePair (','! keyValuePair)*
    |
    /*Empty*/
    ;

keyValuePair
    :
    e1=expr ':' e2=expr
    -&gt; ^(KEY_VALUE_PAIR $e1 $e2)
    ;

preInc
    :
    (DPLUS listSelector) =&gt; DPLUS listSelector
    -&gt; ^(PRE_INC listSelector)
    |
    DPLUS ID
    -&gt; ^(PRE_INC ID)
    ;

postInc
    :
    (listSelector DPLUS) =&gt; listSelector DPLUS
    -&gt; ^(POST_INC listSelector)
    |
    ID DPLUS
    -&gt; ^(POST_INC ID)
    ;

preDec
    :
    (DMINUS listSelector) =&gt; DMINUS listSelector
    -&gt; ^(PRE_DEC listSelector)
    |
    DMINUS ID
    -&gt; ^(PRE_DEC ID)
    ;

postDec
    :
    (listSelector DMINUS) =&gt; listSelector DMINUS
    -&gt; ^(POST_DEC listSelector)
    |
    ID DMINUS
    -&gt; ^(POST_DEC ID)
    ;

constant
    :
    STRING
    |
    NUM_INT
    |
    NUM_FLOAT
    |
    'true'
    |
    'false'
    |
    'nil'
    ;

Tim
-------------- next part --------------
An HTML attachment was scrubbed...
URL: <A HREF="http://www.antlr.org/pipermail/antlr-interest/attachments/20070517/f8f452e5/attachment.html">http://www.antlr.org/pipermail/antlr-interest/attachments/20070517/f8f452e5/attachment.html</A> 
</PRE>


<!--endarticle-->
    <HR>
    <P><UL>
        <!--threads-->
	<LI>Previous message: <A HREF="020712.html">[antlr-interest] Filtering using ANTLR
</A></li>
	<LI>Next message: <A HREF="020714.html">[antlr-interest] Parser --&gt; TreeParser: help with bigger	exampleplease?
</A></li>
         <LI> <B>Messages sorted by:</B> 
              <a href="date.html#20713">[ date ]</a>
              <a href="thread.html#20713">[ thread ]</a>
              <a href="subject.html#20713">[ subject ]</a>
              <a href="author.html#20713">[ author ]</a>
         </LI>
       </UL>

<hr>
<a href="http://www.antlr.org/mailman/listinfo/antlr-interest">More information about the antlr-interest
mailing list</a><br>
</body></html>
